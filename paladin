# Paladin Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3.1 2011/12/15 - Initial version based on Ovale default script.
#                    Use paladin profile from sc-430-2, Tier 13H.
#                    Use EJ prot pally rotation.

## defines ##

# Buffs
Define(THEARTOFWAR 59578)
Define(JUDGEMENTSOFTHEPURE 53655)
Define(DIVINEPURPOSE 90174)

# Abilities
Define(AVENGERSSHIELD 31935)
	SpellInfo(AVENGERSSHIELD cd=15)
Define(AVENGINGWRATH 31884)
	SpellInfo(AVENGINGWRATH cd=180)
	SpellAddBuff(AVENGINGWRATH AVENGINGWRATH=20)
Define(CONSECRATE 26573)
	SpellInfo(CONSECRATE cd=8)
Define(CRUSADERSTRIKE 35395)
	SpellInfo(CRUSADERSTRIKE cd=4 holy=1)
Define(DIVINEFAVOR 31842)
	SpellInfo(DIVINEFAVOR cd=180)
	SpellAddBuff(DIVINEFAVOR DIVINEFAVOR=20)
Define(DIVINEPLEA 54428)
	SpellInfo(DIVINEPLEA cd=120)
	SpellAddBuff(DIVINEPLEA DIVINEPLEA=9)
Define(DIVINEPROTECTION 498)
	SpellInfo(DIVINEPROTECTION cd=60)
	SpellAddBuff(DIVINEPROTECTION DIVINEPROTECTION=10)
Define(DIVINESTORM 53385)
	SpellInfo(DIVINESTORM cd=4.5)
Define(EXORCISM 879)
	SpellAddBuff(EXORCISM THEARTOFWAR=0)
Define(GUARDIANOFANCIENTKINGS 86150)
	SpellInfo(GUARDIANOFANCIENTKINGS cd=300)
Define(HAMMEROFWRATH 24275)
	SpellInfo(HAMMEROFWRATH cd=6)
Define(HAMMEROFTHERIGHTEOUS 53595)
	SpellInfo(HAMMEROFTHERIGHTEOUS cd=4.5 holy=1)
Define(HOLYSHIELD 20925)
	SpellInfo(HOLYSHIELD  cd=30)
	SpellAddBuff(HOLYSHIELD HOLYSHIELD=10)
Define(HOLYSHOCK 20473)
	SpellInfo(HOLYSHOCK cd=6)
Define(HOLYWRATH 2812)
	SpellInfo(HOLYWRATH cd=15)
Define(INQUISITION 84963)
	SpellInfo(INQUISITION holy=-3)
	SpellAddBuff(INQUISITION INQUISITION=12 DIVINEPURPOSE=0)
Define(JUDGEMENT 20271)
	SpellInfo(JUDGEMENT cd=8)
Define(RIGHTEOUSFURY 25780)
Define(SEALRIGHTEOUSNESS 20154)
	SpellAddBuff(SEALRIGHTEOUSNESS SEALRIGHTEOUSNESS=1800)
Define(SEALOFTRUTH 31801)
	SpellAddBuff(SEALOFTRUTH SEALOFTRUTH=1800)
Define(SHIELDOFTHERIGHTEOUS 53600)
	SpellInfo(SHIELDOFTHERIGHTEOUS cd=6 holy=-3)
	SpellAddBuff(SHIELDOFTHERIGHTEOUS DIVINEPURPOSE=0)
Define(TEMPLARSVERDICT 85256)
	SpellInfo(TEMPLARSVERDICT holy=-3)
	SpellAddBuff(TEMPLARSVERDICT DIVINEPURPOSE=0)
Define(WORDOFGLORY 85673)
	SpellInfo(WORDOFGLORY holy=-3)
Define(ZEALOTRY 85696)
	SpellInfo(ZEALOTRY cd=120)
	SpellAddBuff(ZEALOTRY ZEALOTRY=20)

# Items
Define(EARTHENPOTION 58090)
Define(GOLEMBLOODPOTION 58146)

## end defines ##

ScoreSpells(SHIELDOFTHERIGHTEOUS JUDGEMENT AVENGERSSHIELD HAMMEROFTHERIGHTEOUS CONSECRATE HOLYWRATH
			ZEALOTRY INQUISITION TEMPLARSVERDICT DIVINESTORM EXORCISM HAMMEROFWRATH JUDGEMENT CRUSADERSTRIKE)

AddCheckBox(aoe L(AOE))
AddCheckBox(protectionpotions SpellName(EARTHENPOTION) mastery=2)
AddCheckBox(retributionpotions SpellName(GOLEMBLOODPOTION) mastery=3)

AddIcon help=main mastery=1
{
	if HolyPower(more 0) and BuffExpires(INQUISITION) Spell(INQUISITION)
	if ManaPercent(less 90) Spell(DIVINEPLEA)
	if BuffExpires(JUDGEMENTSOFTHEPURE 2) Spell(JUDGEMENT)
	Spell(HOLYSHOCK)
	Spell(EXORCISM)
}

AddIcon help=cd mastery=1
{
	Spell(GUARDIANOFANCIENTKINGS)
	Spell(AVENGINGWRATH)
	Spell(DIVINEFAVOR)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=2
{
	# From the ElitistJerks thread "[Prot] 4.3 - Send me on my way":
	#     http://elitistjerks.com/f76/t126438-prot_4_3_send_me_my_way/#post2013790
	# Single Target Queue:
	# SotR > CS > AS > HoW(if active) > J > Cons* > HW* 
	# (*=mana permitting, boss stationary for Cons to be fully effective)

	if BuffExpires(RIGHTEOUSFURY) Spell(RIGHTEOUSFURY)
	unless InCombat() if BuffExpires(SEALRIGHTEOUSNESS 400) and BuffExpires(SEALOFTRUTH 400) Spell(SEALOFTRUTH)

	if HolyPower(more 2) Spell(SHIELDOFTHERIGHTEOUS)
	Spell(CRUSADERSTRIKE)
	Spell(AVENGERSSHIELD)
	Spell(HAMMEROFWRATH usable=1)
	Spell(JUDGEMENT)
	unless Speed(more 0) Spell(CONSECRATE)
	Spell(HOLYWRATH)
	Spell(DIVINEPLEA priority=2)
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	# From the ElitistJerks thread "[Prot] 4.3 - Send me on my way":
	#     http://elitistjerks.com/f76/t126438-prot_4_3_send_me_my_way/#post2013790
	# HotR and Inquisition are the bulk of our AoE DPS. The queue that seems to maximize our multi-target output is:
	# Inq(if not active) > HotR > AS > Cons > HW > J

	if BuffExpires(RIGHTEOUSFURY) Spell(RIGHTEOUSFURY)
	unless InCombat() if BuffExpires(SEALRIGHTEOUSNESS 400) and BuffExpires(SEALOFTRUTH 400) Spell(SEALOFTRUTH)

	unless BuffPresent(INQUISITION Spell(INQUISITION usable=1)
	Spell(HAMMEROFTHERIGHTEOUS)
	Spell(AVENGERSSHIELD)
	Spell(CONSECRATE)
	Spell(HOLYWRATH)
	Spell(JUDGEMENT)
}

AddIcon help=cd mastery=2
{
	unless InCombat() if CheckBoxOn(protectionpotions) Item(EARTHENPOTION)
	if TargetIsInterruptible() and TargetInRange(REBUKE) Spell(REBUKE)
	if HolyPower(more 2) Spell(WORDOFGLORY)
	Spell(HOLYSHIELD)
	Spell(GUARDIANOFANCIENTKINGS)
	Spell(AVENGINGWRATH)
	Spell(DIVINEPROTECTION)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=3
{
	unless InCombat() if BuffExpires(SEALRIGHTEOUSNESS 400) and BuffExpires(SEALOFTRUTH 400) Spell(SEALOFTRUTH)

	#/seal_of_truth
	unless BuffPresent(SEALOFRIGHTEOUSNESS) or BuffPresent(SEALOFTRUTH) Spell(SEALOFTRUTH)
	#/judgement,if=buff.judgements_of_the_pure.down
	unless BuffPresent(JUDGEMENTSOFTHEPURE) Spell(JUDGEMENT)
	#/inquisition,if=(buff.inquisition.down|buff.inquisition.remains<5)&(holy_power=3|buff.divine_purpose.react)
	if BuffExpires(INQUISITION 5) and {HolyPower(equal 3) or BuffPresent(DIVINEPURPOSE)} Spell(INQUISITION)
	#/crusader_strike,if=holy_power<3
	if HolyPower(less 3)
	{
		if CheckBoxOff(aoe) Spell(CRUSADERSTRIKE)
		if CheckBoxOn(aoe) Spell(DIVINESTORM)
	}
	#/judgement,if=set_bonus.tier13_2pc_melee&holy_power<3
	if HolyPower(less 3) and ArmorSetParts(T13 more 1) Spell(JUDGEMENT)
	#/templars_verdict,if=buff.divine_purpose.react
	if BuffPresent(DIVINEPURPOSE) Spell(TEMPLARSVERDICT)
	#/templars_verdict,if=holy_power=3
	if HolyPower(equal 3) Spell(TEMPLARSVERDICT)
	#/exorcism,if=buff.the_art_of_war.react
	if BuffPresent(THEARTOFWAR) Spell(EXORCISM)
	#/hammer_of_wrath
	Spell(HAMMEROFWRATH)
	#/judgement
	Spell(JUDGEMENT)
	#/holy_wrath
	Spell(HOLYWRATH)
	#/consecration,if=mana>17000
	If Mana(more 17000) Spell(CONSECRATE)
	#/divine_plea
	Spell(DIVINEPLEA)
}

AddIcon help=cd mastery=3
{
	#/golemblood_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(retributionpotions) Item(GOLEMBLOODPOTION)
	if TargetIsInterruptible() and TargetInRange(REBUKE) Spell(REBUKE)
	#/golemblood_potion,if=buff.bloodlust.react
	if CheckBoxOn(retributionpotions) and {BuffPresent(heroism) or TargetDeadIn(less 40)} Item(GOLEMBLOODPOTION)
	#/zealotry
	Spell(ZEALOTRY)
	#/guardian_of_ancient_kings,if=(buff.zealotry.remains<31&buff.zealotry.up)|cooldown.zealotry.remains>60
	if BuffExpires(ZEALOTRY 31) or {spell(ZEALOTRY) > 60} Spell(GUARDIANOFANCIENTKINGS)
	#/avenging_wrath,if=buff.zealotry.remains<21&buff.zealotry.up
	if BuffExpires(ZEALOTRY 21) Spell(AVENGINGWRATH)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}