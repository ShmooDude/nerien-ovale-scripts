# Hunter Ovale script based on SimulationCraft profiles
#
# Revision History
#
# To Do:
# - Check rotations for 2pT13 and 4pT13
# - Properly deal with the mechanics for Improved Steady Shot (every 2 SS in a row proc ISS)

## defines ##

# Buffs
Define(CHRONOHUNTER 105919) # 4pT13 buff
Define(FIRE 82926)
Define(FRENZYEFFECT 19615)
Define(LOCKANDLOAD 56453)
Define(THEBEASTWITHIN 34692)

# Debuffs
Define(MARKEDFORDEATH 88691)

# Glyphs
Define(GLYPHOFARCANESHOT 56995)
Define(GLYPHOFTRAPLAUNCHER 56996)

# Spells
Define(AIMEDSHOT 19434)
	SpellInfo(AIMEDSHOT resetcounter=ss mana=50)
	SpellAddBuff(AIMEDSHOT FIRE=0)
Define(ARCANESHOT 3044)
	SpellInfo(ARCANESHOT resetcounter=ss mana=25)
Define(ASPECTOFTHEFOX 82661)
	SpellAddBuff(ASPECTOFTHEFOX ASPECTOFTHEFOX=1000)
Define(ASPECTOFTHEHAWK 13165)
	SpellAddBuff(ASPECTOFTHEHAWK ASPECTOFTHEHAWK=1000)
Define(BESTIALWRATH 19574)
	SpellInfo(BESTIALWRATH cd=120)
	SpellAddBuff(BESTIALWRATH BESTIALWRATH=10)
Define(BLACKARROW 3674)
	SpellInfo(BLACKARROW cd=30 mana=35)
	SpellAddTargetDebuff(BLACKARROW BLACKARROW=15)
Define(CALLOFTHEWILD 53434)
	SpellInfo(CALLOFTHEWILD cd=300)
	SpellAddBuff(CALLOFTHEWILD CALLOFTHEWILD=20)
Define(CHIMERASHOT 53209)
	SpellInfo(CHIMERASHOT cd=10 resetcounter=ss mana=50)
	SpellAddTargetDebuff(CHIMERASHOT SERPENTSTING=refresh)
Define(COBRASHOT 77767)
	SpellInfo(COBRASHOT mana=-9)
	SpellAddTargetDebuff(COBRASHOT SERPENTSTING=refresh)
Define(EXPLOSIVESHOT 53301)
	SpellInfo(EXPLOSIVESHOT cd=6 mana=50 buffnocd=LOCKANDLOAD)
	SpellAddTargetDebuff(EXPLOSIVESHOT EXPLOSIVESHOT=2)
	SpellAddBuff(EXPLOSIVESHOT LOCKANDLOAD=-1)
Define(EXPLOSIVETRAPLAUNCHER 82939)
	SpellInfo(EXPLOSIVETRAPLAUNCHER cd=30)
Define(FERVOR 82726)
	SpellInfo(FERVOR mana=-50 cd=120)
Define(FOCUSFIRE 82692)
	SpellInfo(FOCUSFIRE cd=15)
	SpellAddBuff(FOCUSFIRE FOCUSFIRE=15)
Define(KILLCOMMAND 34026)
	SpellInfo(KILLCOMMAND cd=6 mana=40 resetcounter=ss)
Define(KILLSHOT 53351)
	SpellInfo(KILLSHOT cd=10 resetcounter=ss)
Define(HUNTERSMARK 1130)
	SpellAddTargetDebuff(HUNTERSMARK HUNTERSMARK=300)
Define(MULTISHOT 2643)
	SpellInfo(MULTISHOT mana=40)
Define(RAPIDFIRE 3045)
	SpellAddBuff(RAPIDFIRE RAPIDFIRE=15)
	SpellInfo(RAPIDFIRE cd=300 resetcounter=ss)
Define(READINESS 23989)
	SpellInfo(READINESS cd=180)
Define(SERPENTSTING 1978)
	SpellInfo(SERPENTSTING resetcounter=ss duration=15 mana=25)
	SpellAddTargetDebuff(SERPENTSTING SERPENTSTING=15)
Define(STEADYSHOT 56641)
	SpellInfo(STEADYSHOT inccounter=ss mana=-9)
Define(SILENCINGSHOT 34490)
	SpellInfo(SILENCINGSHOT cd=20 resetcounter=ss)
Define(TRAPLAUNCHER 77769)
	SpellInfo(TRAPLAUNCHER cd=1.5 mana=20)
	SpellInfo(TRAPLAUNCHER addmana=-10 glyph=56996) # Glyph of Trap Launcher
	SpellAddBuff(TRAPLAUNCHER TRAPLAUNCHER=15)

# Pet spells
Define(GROWL 2649)

# Items
Define(POTIONOFTHETOLVIR 58145)
	Define(POTIONOFTHETOLVIRSPELL 80495)

## end defines ##

ScoreSpells(FOCUSFIRE KILLCOMMAND ARCANESHOT KILLSHOT STEADYSHOT SERPENTSTING CHIMERASHOT AIMEDSHOT
			BLACKARROW EXPLOSIVESHOT)

AddCheckBox(aoe L(aoe))
AddCheckBox(potions SpellName(POTIONOFTHETOLVIRSPELL))

AddIcon help=main mastery=1
{
	#/serpent_sting,if=!ticking
	unless BuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/kill_command
	if TargetInRange(GROWL) Spell(KILLCOMMAND)
	#/arcane_shot,if=focus>=59|buff.beast_within.up
	if Mana(more 59) or BuffPresent(THEBEASTWITHIN) Spell(ARCANESHOT)
	#/cobra_shot
	Spell(COBRASHOT)
}

AddIcon help=offgcd mastery=1
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	Spell(CALLOFTHEWILD usable=1)
	#/focus_fire,five_stacks=1
	if pet.BuffPresent(FRENZYEFFECT stacks=5) Spell(FOCUSFIRE)
	#/bestial_wrath,if=focus>60
	if Mana(more 60) Spell(BESTIALWRATH)
}

AddIcon help=aoe mastery=1 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	#/serpent_sting,if=!ticking
	unless BuffPresent(SERPENTSTING mine=1) if Mana(more 24) Spell(SERPENTSTING)
	#/multi_shot,if=target.adds>5
	if Mana(more 39) Spell(MULTISHOT)
	#/cobra_shot,if=target.adds>5
	Spell(COBRASHOT)
}

AddIcon help=cd mastery=1
{
	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/hunters_mark
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 20) Spell(HUNTERSMARK nored=1)
	#/summon_pet
	#if PetPresent(no) Spell(CALLPET)
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60)} Item(POTIONOFTHETOLVIR)
	#/rapid_fire,if=!buff.bloodlust.up&!buff.beast_within.up
	unless BuffPresent(heroism) or BuffPresent(THEBEASTWITHIN) Spell(RAPIDFIRE)
	#/fervor,if=focus<=37
	if Mana(less 37) Spell(FERVOR)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}

AddIcon help=main mastery=2
{
	#/serpent_sting,if=!ticking&target.health_pct<=90
	unless TargetDebuffPresent(SERPENTSTING) if TargetLifePercent(less 90) and Mana(more 24) Spell(SERPENTSTING)
	#/chimera_shot,if=target.health_pct<=90
	if TargetLifePercent(less 90) and Mana(more 49) Spell(CHIMERASHOT)
	#/steady_shot,if=buff.pre_improved_steady_shot.up&buff.improved_steady_shot.remains<3
	if Counter(ss equal 1) and BuffExpires(IMPROVEDSTEADYSHOT 3) Spell(STEADYSHOT)
	#/kill_shot
	Spell(KILLSHOT usable=1)
	#/aimed_shot,if=buff.master_marksman_fire.react
	if BuffPresent(FIRE) Spell(AIMEDSHOT)
	if Glyph(GLYPHOFARCANESHOT) or ArmorSetParts(T13 more 3)
	{
		#/arcane_shot,if=(focus>=66|cooldown.chimera_shot.remains>=4)&\
		#	(target.health_pct<90&!buff.rapid_fire.up&!buff.bloodlust.react&!buff.berserking.up&\
		#		!buff.tier13_4pc.react&cooldown.buff_tier13_4pc.remains<=0)
		unless Mana(less 66) and 5s before Spell(CHIMERASHOT)
			unless BuffPresent(RAPIDFIRE) or BuffPresent(heroism) or BuffPresent(CHRONOHUNTER) # or 4pT13 ICD > 0
				Spell(ARCANESHOT)
	}
	#/aimed_shot,if=(cooldown.chimera_shot.remains>5|focus>=80)&(buff.bloodlust.react|buff.tier13_4pc.react|cooldown.buff_tier13_4pc.remains>0)|\
	#	buff.rapid_fire.up|target.health_pct>90
	if Mana(more 49)
	{
		unless 5s before Spell(CHIMERASHOT) and Mana(less 80)
			if BuffPresent(heroism) or BuffPresent(CHRONOHUNTER) # or 4pT13 ICD > 0
				Spell(AIMEDSHOT)
		if BuffPresent(RAPIDFIRE) or TargetLifePercent(more 90) Spell(AIMEDSHOT)
	}
	#/steady_shot
	Spell(STEADYSHOT)
}

AddIcon help=offgcd mastery=2
{
	unless InCombat() unless BuffPresent(ASPECTOFTHEHAWK) or BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_hawk,moving=0
	unless Speed(more 0) unless BuffPresent(ASPECTOFTHEHAWK) Spell(ASPECTOFTHEHAWK)
	#/aspect_of_the_fox,moving=1
	if Speed(more 0) unless BuffPresent(ASPECTOFTHEFOX) Spell(ASPECTOFTHEFOX)
	Spell(CALLOFTHEWILD usable=1)
}

AddIcon help=aoe mastery=2 checkboxon=aoe
{
	#/explosive_trap,if=target.adds>0
	if BuffPresent(TRAPLAUNCHER) Spell(EXPLOSIVETRAPLAUNCHER)
	#/multi_shot,if=target.adds>5
	if Mana(more 39) Spell(MULTISHOT)
	#/steady_shot,if=target.adds>5
	Spell(STEADYSHOT)
}

AddIcon help=cd mastery=2
{
	#/tolvir_potion,if=!in_combat
	unless InCombat() if CheckBoxOn(potions) Item(POTIONOFTHETOLVIR)
	#/hunters_mark
	unless TargetDebuffPresent(HUNTERSMARK) or TargetDebuffPresent(MARKEDFORDEATH) if TargetDeadIn(more 20) Spell(HUNTERSMARK nored=1)
	#/summon_pet
	#if PetPresent(no) Spell(CALLPET)
	#/tolvir_potion,if=!in_combat|buff.bloodlust.react|target.time_to_die<=60|buff.rapid_fire.react
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 60) or BuffPresent(RAPIDFIRE)} Item(POTIONOFTHETOLVIR)
	#/rapid_fire,if=!buff.bloodlust.up|target.time_to_die<=30
	unless BuffPresent(heroism) and TargetDeadIn(more 30) Spell(RAPIDFIRE)
	#/readiness,wait_for_rapid_fire=1
	if BuffPresent(RAPIDFIRE) Spell(READINESS)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
}