### Shadow Priest Ovale script based on SimulationCraft profile
### https://sites.google.com/site/wownerien/ovale/priest/script
### Version: 4.3.7
#
# Shadow:
#	talents=http://www.wowhead.com/talent#priest-032212000000000000000000000000000000000000322032210201222100231
#	glyphs=mind_flay/shadow_word_pain/shadow_word_death/spirit_tap

## defines ##

# Buffs
Define(DARKARCHANGEL 87153)
Define(EMPOWEREDSHADOW 95799)
Define(EVANGELISM 87118)
SpellList(MINDMELTBUFF 81292 87160)
Define(MINDSPIKEBUFF 87178)
Define(POWERWORDFORTITUDEBUFF 79105)
Define(SHADOWORB 77487)
SpellList(STAMINABUFF 469 6309 79105 90364) # Commanding Shout, Blood Pact, Power Word: Fortitude, Qiraji Fortitude

# Glyphs
Define(GLYPHOFDISPERSION 63229)

# Talents
Define(IMPROVEDDEVOURINGPLAGUETALENT 9062)
Define(IMPROVEDMINDBASTTALENT 9042)
Define(MASOCHISMTALENT 11778)
Define(MINDMELTTALENT 9060)
Define(SHADOWFORMTALENT 9064)
Define(SHADOWYAPPARITIONTALENT 9070)
Define(VAMPIRICEMBRACETALENT 9054)
Define(VAMPIRICTOUCHTALENT 9074)

# Spells
Define(ARCHANGEL 87151)
	SpellInfo(ARCHANGEL cd=90)
	SpellAddBuff(ARCHANGEL DARKARCHANGEL=18)
Define(DEVOURINGPLAGUE 2944)
	SpellInfo(DEVOURINGPLAGUE duration=24 tick=3)
	SpellAddTargetDebuff(DEVOURINGPLAGUE DEVOURINGPLAGUE=24)
Define(DISPERSION 47585)
	SpellInfo(DISPERSION cd=120)
	SpellInfo(DISPERSION addcd=-45 glyph=GLYPHOFDISPERSION)
Define(INNERFIRE 588)
	SpellAddBuff(INNERFIRE INNERFIRE=86400)	# fake duration
Define(INNERWILL 73413)
	SpellAddBuff(INNERWILL INNERWILL=86400)	# fake duration
Define(MINDBLAST 8092)
	SpellInfo(MINDBLAST cd=8)
	SpellInfo(MINDBLAST addcd=-2 talent=IMPROVEDMINDBASTTALENT)
	SpellAddBuff(MINDBLAST EMPOWEREDSHADOW=15 SHADOWORB=0)
Define(MINDFLAY 15407)
	SpellInfo(MINDFLAY canStopChannelling=3)
Define(MINDSPIKE 73510)
	SpellAddBuff(MINDSPIKE MINDSPIKEBUFF=12 SHADOWORB=0)
Define(POWERWORDFORTITUDE 21562)
	SpellAddBuff(POWERWORDFORTITUDE POWERWORDFORTITUDEBUFF=3600)
Define(SHADOWFIEND 34433)
	SpellInfo(SHADOWFIEND cd=300)
Define(SHADOWFORM 15473)
Define(SHADOWWORDDEATH 32379)
	SpellInfo(SHADOWWORDDEATH cd=10)
Define(SHADOWWORDPAIN 589)
	SpellInfo(SHADOWWORDPAIN duration=18 tick=3)
	SpellAddTargetDebuff(SHADOWWORDPAIN SHADOWWORDPAIN=18)
Define(SILENCE 15487)
	SpellInfo(SILENCE cd=45)
Define(VAMPIRICEMBRACE 15286)
	SpellAddBuff(VAMPIRICEMBRACE VAMPIRICEMBRACE=86400)	# fake duration
Define(VAMPIRICTOUCH 34914)
	SpellInfo(VAMPIRICTOUCH duration=15 tick=3)
	SpellAddTargetDebuff(VAMPIRICTOUCH VAMPIRICTOUCH=15)

# Racials & Professions
Define(ARCANETORRENTMANA 28730) # blood elf
	SpellInfo(ARCANETORRENTMANA sharedcd=racial cd=120)
Define(BERSERKING 26297) # troll
	SpellInfo(BERSERKING duration=10 cd=180)
	SpellAddBuff(BERSERKING BERSERKING=10)
Define(LIFEBLOOD 55503) # herbalism
	SpellInfo(LIFEBLOOD duration=20 cd=120)
Define(STONEFORM 20594) # dwarf
	SpellInfo(STONEFORM duration=8 cd=120)
	SpellAddBuff(STONEFORM STONEFORM=8)

# Items
Define(VOLCANICPOTION 58091)
	Define(VOLCANICPOTIONSPELL 80481)

### end defines ###

ScoreSpells(MINDBLAST SHADOWWORDPAIN VAMPIRICTOUCH DEVOURINGPLAGUE MINDFLAY SHADOWWORDDEATH MINDSPIKE)

AddCheckBox(potions SpellName(VOLCANICPOTIONSPELL) default)

# Trinket CDs
AddListItem(trinketcd0 000s "Trinket 0 CD - none" default)
AddListItem(trinketcd0 060s "Trinket 0 CD - 1 min")
AddListItem(trinketcd0 090s "Trinket 0 CD - 1 min 30s")
AddListItem(trinketcd0 120s "Trinket 0 CD - 2 min")
AddListItem(trinketcd1 000s "Trinket 1 CD - none" default)
AddListItem(trinketcd1 060s "Trinket 1 CD - 1 min")
AddListItem(trinketcd1 090s "Trinket 1 CD - 1 min 30s")
AddListItem(trinketcd1 120s "Trinket 1 CD - 2 min")

AddFunction UseItemActions
{
	Item(HandsSlot usable=1)
	unless List(trinketcd0 000s) Item(Trinket0Slot usable=1)
	unless List(trinketcd1 000s) Item(Trinket1Slot usable=1)
}

AddFunction UseProfessionActions
{
	Spell(LIFEBLOOD)
}

AddFunction UseRacialActions
{
	if ManaPercent(less 91) Spell(ARCANETORRENTMANA)
	Spell(BERSERKING)
}

###
### Shadow
###

# Movement.
AddIcon size=small mastery=3
{
	if TalentPoints(SHADOWYAPPARITIONTALENT more 0) and TargetDebuffPresent(SHADOWWORDPAIN mine=1)
	{
		#/stop_moving,health_percentage<=25,if=cooldown.shadow_word_death.remains>=0.2|dot.vampiric_touch.remains<cast_time+2.5
		#/start_moving,health_percentage<=25,if=cooldown.shadow_word_death.remains<=0.1
		if TargetLifePercent(less 25)
			and 0.2s before Spell(SHADOWWORDDEATH)
			and TalentPoints(VAMPIRICTOUCHTALENT more 0) and TargetDebuffPresent(VAMPIRICTOUCH 4.5 mine=1 haste=spell)
		{
			Texture(ability_druid_dash)
		}
	}
}

# Devouring Plague (can only be placed on a single target at a time).
AddIcon help=buff size=small mastery=3
{
	unless BuffExpires(STAMINABUFF) or {InCombat(no) and BuffExpires(STAMINABUFF 400)}
		or BuffExpires(INNERFIRE) and BuffExpires(INNERWILL)
		or {TalentPoints(SHADOWFORMTALENT more 0) and BuffExpires(SHADOWFORM)}
		or {TalentPoints(VAMPIRICEMBRACETALENT more 0) and BuffExpires(VAMPIRICEMBRACE)}
		or {Speed(equal 0) and Spell(MINDBLAST)}
		or TargetDebuffExpires(SHADOWWORDPAIN 2.5 mine=1 haste=spell)
	{
		#/devouring_plague,if=(!ticking|dot.devouring_plague.remains<gcd+1.0)&miss_react
		if TargetDebuffExpires(DEVOURINGPLAGUE 3 mine=1 haste=spell) Spell(DEVOURINGPLAGUE)
	}
}

# Main rotation.
AddIcon help=main mastery=3
{
	#/fortitude
	if BuffExpires(STAMINABUFF) or {InCombat(no) and BuffExpires(STAMINABUFF 400)} Spell(POWERWORDFORTITUDE)
	#/inner_fire
	if BuffExpires(INNERFIRE) and BuffExpires(INNERWILL) Spell(INNERFIRE)
	#/shadow_form
	if BuffExpires(SHADOWFORM) Spell(SHADOWFORM)
	#/vampiric_embrace
	if BuffExpires(VAMPIRICEMBRACE) Spell(VAMPIRICEMBRACE)
	#/mind_blast
	if Spell(MINDBLAST)
	#/shadow_word_pain,if=(!ticking|dot.shadow_word_pain.remains<gcd+0.5)&miss_react
	if TargetDebuffExpires(SHADOWWORDPAIN 2.5 mine=1 haste=spell) Spell(SHADOWWORDPAIN)
	#/vampiric_touch,if=(!ticking|dot.vampiric_touch.remains<cast_time+2.5)&miss_react
	if TargetDebuffExpires(VAMPIRICTOUCH 4.5 mine=1 haste=spell) Spell(VAMPIRICTOUCH)
	#/shadow_word_death,health_percentage<=25
	if TalentPoints(MINDMELTTALENT more 0) and TargetLifePercent(less 25) Spell(SHADOWWORDDEATH)
	#/shadow_word_death,if=mana_pct<10
	if TalentPoints(MASOCHISMTALENT more 0) and ManaPercent(less 10) Spell(SHADOWWORDDEATH)
	#/mind_flay
	Spell(MINDFLAY)
}

# Movement & Mind Spike rotation.
AddIcon help=main mastery=3
{
	# Movement.
	if Speed(more 0)
	{
		#/shadow_word_pain,if=(!ticking|dot.shadow_word_pain.remains<gcd+0.5)&miss_react
		if TargetDebuffExpires(SHADOWWORDPAIN 2.5 mine=1 haste=spell) Spell(SHADOWWORDPAIN)
		#/shadow_word_death,moving=1
		Spell(SHADOWWORDDEATH)
		#/devouring_plague,moving=1,if=mana_pct>10
		if TalentPoints(IMPROVEDDEVOURINGPLAGUETALENT more 0) and ManaPercent(more 10) Spell(DEVOURINGPLAGUE)
	}

	# Mind Spike rotation on short-lived mobs.
	if TargetDeadIn(less 15)
	{
		if TalentPoints(MINDMELTTALENT more 0) and TargetLifePercent(less 25) Spell(SHADOWWORDDEATH)
		if BuffPresent(MINDSPIKEBUFF stacks=3) Spell(MINDBLAST) # guaranteed MB crit
		if BuffPresent(MINDMELTBUFF stacks=2) Spell(MINDBLAST) # instant-cast MB
		Spell(MINDSPIKE)
		Spell(MINDFLAY)
	}
}

# Long CDs.
AddIcon help=cd mastery=3
{
	#/volcanic_potion,if=!in_combat
	if InCombat(no) and CheckBoxOn(potions) and TargetClassification(worldboss) Item(VOLCANICPOTION)
	if TargetIsInterruptible(yes) Spell(SILENCE)

	unless BuffExpires(STAMINABUFF) or {InCombat(no) and BuffExpires(STAMINABUFF 400)}
		or BuffExpires(INNERFIRE) and BuffExpires(INNERWILL)
		or {TalentPoints(SHADOWFORMTALENT more 0) and BuffExpires(SHADOWFORM)}
		or {TalentPoints(VAMPIRICEMBRACETALENT more 0) and BuffExpires(VAMPIRICEMBRACE)}
	{
		UseItemActions()
		UseProfessionActions()
		#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
		if CheckBoxOn(potions) and TargetClassification(worldboss) and {BuffPresent(heroism) or TargetDeadIn(less 40)} Item(VOLCANICPOTION)
		unless Spell(MINDBLAST)
		{
			#/berserking
			UseRacialActions()
			unless TargetDebuffExpires(SHADOWWORDPAIN 2.5 mine=1 haste=spell)
				or {TalentPoints(VAMPIRICTOUCHTALENT more 0) and TargetDebuffExpires(VAMPIRICTOUCH 4.5 mine=1 haste=spell)}
			{
				#/archangel,if=buff.dark_evangelism.stack>=5&dot.vampiric_touch.remains>5&dot.devouring_plague.remains>5
				if BuffPresent(EVANGELISM stacks=5)
					and TargetDebuffPresent(VAMPIRICTOUCH 5 mine=1) and TargetDebuffPresent(DEVOURINGPLAGUE 5 mine=1)
				{
					Spell(ARCHANGEL)
				}
				unless TalentPoints(MINDMELTTALENT more 0) and TargetLifePercent(less 25) and Spell(SHADOWWORDDEATH)
				{
					#/shadow_fiend
					Spell(SHADOWFIEND)
				}
			}
		}
	}
}

# Dispersion.
AddIcon help=cd size=small mastery=3
{
	#/dispersion
	Spell(DISPERSION)
}