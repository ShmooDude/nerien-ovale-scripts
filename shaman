# Shaman Ovale script based on SimulationCraft profiles
#
# Revision History
# 4.3.1 2011/12/07 - Initial version based on Ovale default script.
#                    Use shaman profiles from sc-430-2, Tier 12N.

## defines ##

# Buffs
Define(MAELSTROMWEAPON 53817)
Define(UNLEASHWIND 73681)

# Glyphs
Define(GLYPHOFGROUNDINGTOTEM 57247)
Define(GLYPHOFSTONECLAWTOTEM 64247)
Define(GLYPHOFUNLEASHEDLIGHTNING 101052)

# Talents
Define(FULMINATIONTALENT ???)
Define(REVERBERATIONTALENT ???)

# Spells
Define(BLOODLUST 2825)
	SpellInfo(BLOODLUST cd=300)
	SpellAddBuff(BLOODLUST BLOODLUST=40)
Define(CHAINLIGHTNING 421)
	SpellAddBuff(CHAINLIGHTNING MAELSTROMWEAPON=0)
Define(EARTHQUAKE 61882)
Define(EARTHSHOCK 8042)
	SpellInfo(EARTHSHOCK cd=6 sharedcd=shock)
	SpellInfo(EARTHSHOCK addcd=-1 talent=???) # Reverberation (Rank 2)
	SpellAddTargetDebuff(EARTHSHOCK EARTHSHOCK=8)
Define(ELEMENTALMASTERY 16166)
	SpellInfo(ELEMENTALMASTERY cd=180)
	SpellAddBuff(ELEMENTALMASTERY ELEMENTALMASTERY=30)
Define(FERALSPIRIT 51533)
	SpellInfo(FERALSPIRIT cd=120)
Define(FIRENOVA 1535)
	SpellInfo(FIRENOVA cd=10)
Define(FLAMESHOCK 8050)
	SpellInfo(FLAMESHOCK cd=6 sharedcd=shock duration=18)
	SpellInfo(FLAMESHOCK addcd=-1 talent=???) # Reverberation (Rank 2)
	SpellInfo(FLAMESHOCK addduration=9 glyph=57239) # Glyph of Flame Shock
	SpellAddTargetDebuff(FLAMESHOCK FLAMESHOCK=18)
Define(FLAMETONGUEWEAPON 8024)
Define(FROSTSHOCK 8056)
	SpellInfo(FROSTSHOCK cd=6 sharedcd=shock)
	SpellInfo(FROSTSHOCK addcd=-1 talent=???) # Reverberation (Rank 2)
	SpellAddTargetDebuff(FROSTSHOCK FROSTSHOCK=8)
Define(FROSTBRANDWEAPON 8033)
Define(HEROISM 32182)
	SpellInfo(HEROISM cd=300)
	SpellAddBuff(HEROISM HEROISM=40)
Define(LAVABURST 51505)
	SpellInfo(LAVABURST cd=8)
Define(LAVALASH 60103)
	SpellInfo(LAVALASH cd=10)
Define(LIGHTNINGBOLT 403)
	SpellAddBuff(LIGHTNINGBOLT MAELSTROMWEAPON=0)
Define(LIGHTNINGSHIELD 324)
	SpellAddBuff(LIGHTNINGSHIELD LIGHTNINGSHIELD=600)
Define(PRIMALSTRIKE 73899)
	SpellInfo(PRIMALSTRIKE cd=8)
Define(ROCKBITERWEAPON 8017)
Define(SHAMANISTICRAGE 30823)
	SpellInfo(SHAMANISTICRAGE cd=60)
	SpellAddBuff(SHAMANISTICRAGE SHAMANISTICRAGE=15)
Define(SPIRITWALKERSGRACE 79206)
	SpellInfo(SPIRITWALKERSGRACE cd=120)
	SpellAddBuff(SPIRITWALKERSGRACE SPIRITWALKERSGRACE=15)
Define(STORMSTRIKE 17364)
	SpellInfo(STORMSTRIKE cd=8)
	SpellAddTargetDebuff(STORMSTRIKE STORMSTRIKE=15)
Define(THUNDERSTORM 51490)
	SpellInfo(THUNDERSTORM cd=45)
Define(UNLEASHELEMENTS 73680)
	SpellInfo(UNLEASHELEMENTS cd=15)
Define(UNLEASHFLAME 73683)
	SpellInfo(UNLEASHFLAME cd=15)
	SpellAddBuff(UNLEASHFLAME UNLEASHFLAME=8)
Define(WATERSHIELD 52127)
	SpellAddBuff(WATERSHIELD WATERSHIELD=600)
Define(WINDFURYWEAPON 8232)
Define(WINDSHEAR 57994)
	SpellInfo(WINDSHEAR cd=15)
	SpellInfo(WINDSHEAR addcd=-10 talent=???) # Reverberation (Rank 2)

# Fire totems
Define(FIREELEMENTALTOTEM 2894)
Define(FLAMETONGUETOTEM 8227)
Define(MAGMATOTEM 8190)
	SpellInfo(MAGMATOTEM cd=20)
Define(SEARINGTOTEM 3599)
	SpellInfo(SEARINGTOTEM cd=10) # fake CD so it won't be displayed twice
# Water totems
Define(HEALINGSTREAMTOTEM 5394)
Define(MANASPRINGTOTEM 5675)
Define(TOTEMOFTRANQUILMIND 87718)
# Air totems
Define(GROUNDINGTOTEM 8177)
	SpellInfo(GROUNDINGTOTEM cd=25)
	SpellInfo(GROUNDINGTOTEM addcd=35 glyph=57247) # Glyph of Grounding Totem
Define(WINDFURYTOTEM 8512)
Define(WRATHOFAIRTOTEM 3738)
# Earth totems
Define(STONECLAWTOTEM 5730)
	SpellInfo(STONECLAWTOTEM cd=20)
Define(STONESKINTOTEM 8071)
Define(STRENGTHOFEARTHTOTEM 8075)
Define(TREMORTOTEM 8143)
	SpellInfo(TREMORTOTEM cd=60)

# Items
Define(POTIONOFTHETOLVIR 58145)
Define(VOLCANICPOTION 58091)

## end defines ##

ScoreSpells(LIGHTNINGSHIELD CHAINLIGHTNING FLAMESHOCK LAVABURST EARTHSHOCK LIGHTNINGBOLT
			STORMSTRIKE LAVALASH UNLEASHELEMENTS)

AddCheckBox(potions "Use potions")
AddCheckBox(aoe L(AOE))

AddIcon help=main mastery=1
{
	unless InCombat() if WeaponEnchantExpires(mainhand 400) Spell(FLAMETONGUEWEAPON)

	#/flametongue_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	#/unleash_elements,moving=1
	if Speed(more 0) and Glyph(GLYPHOFUNLEASHEDLIGHTNING no) Spell(UNLEASHELEMENTS)
	#/flame_shock,if=!ticking|ticks_remain<2
	if target.debuffExpires(FLAMESHOCK mine=1) < timeWithHaste(3) Spell(FLAMESHOCK)
	#/lava_burst,if=dot.flame_shock.remains>(cast_time+travel_time)
	if target.debuffExpires(FLAMESHOCK mine=1) > {castTime(LAVABURST) + 1} Spell(LAVABURST)
	if TalentPoints(FULMINATIONTALENT more 0)
	{
		#/earth_shock,if=buff.lightning_shield.stack=9
		if BuffPresent(LIGHTNINGSHIELD stacks=9) Spell(EARTHSHOCK)
		#/earth_shock,if=buff.lightning_shield.stack>6&dot.flame_shock.remains>cooldown&\
		#    dot.flame_shock.remains<cooldown+action.flame_shock.tick_time
		if {BuffPresent(LIGHTNINGSHIELD stacks=7) or BuffPresent(LIGHTNINGSHIELD stacks=8)}
			and {target.debuffExpires(FLAMESHOCK mine=1) > spell(EARTHSHOCK)}
			and {target.debuffExpires(FLAMESHOCK mine=1) < spell(EARTHSHOCK) + timeWithHaste(3)}
			Spell(EARTHSHOCK)
	}
	#/searing_totem
	if TotemExpires(fire) Spell(SEARINGTOTEM)
	#/chain_lightning,if=target.adds>2
	if CheckBoxOn(aoe) Spell(CHAINLIGHTNING)
	#/lightning_bolt
	Spell(LIGHTNINGBOLT)
}

AddIcon help=cd mastery=1
{
	#/volcanic_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(VOLCANICPOTION)
	if IsFeared() Spell(TREMORTOTEM)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/volcanic_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(VOLCANICPOTION)
	#/elemental_mastery
	Spell(ELEMENTALMASTERY)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
	#/fire_elemental_totem,if=temporary_bonus.intellect>=1600&(!ticking|remains<25)
	Spell(FIREELEMENTALTOTEM)
	#/earth_elemental_totem,if=!ticking
	Spell(EARTHELEMENTALTOTEM)
	#/spiritwalkers_grace,moving=1
	if Speed(more 0) Spell(SPIRITWALKERSGRACE)
}

AddIcon help=main mastery=2
{
	unless InCombat()
	{
		if WeaponEnchantExpires(mainhand 400) Spell(WINDFURYWEAPON)
		if WeaponEnchantExpires(offhand 400) Spell(FLAMETONGUEWEAPON)
	}

	#/windfury_weapon,weapon=main
	if WeaponEnchantExpires(mainhand 2) Spell(WINDFURYWEAPON)
	#/flametongue_weapon,weapon=off
	if WeaponEnchantExpires(offhand 2) Spell(FLAMETONGUEWEAPON)
	#/lightning_shield
	if BuffExpires(LIGHTNINGSHIELD 2) Spell(LIGHTNINGSHIELD)
	if CheckBoxOn(aoe)
	{
		# Enhancement 4.3 AoE rotation from ElitistJerks:
		#    http://elitistjerks.com/f79/t127416-enhancement_4_3_least_your_old_axe_good_transmog/#post2051733
		if BuffPresent(MAELSTROMWEAPON stacks=5) Spell(CHAINLIGHTNING)
		if TotemExpires(fire) Spell(MAGMATOTEM)
		Spell(UNLEASHELEMENTS)
		if TargetDebuffExpires(FLAMESHOCK 0 mine=1) or BuffPresent(UNLEASHFLAME) Spell(FLAMESHOCK)
		Spell(LAVALASH)
		if OtherDebuffPresent(FLAMESHOCK mine=1) Spell(FIRENOVA)
	}
	#/flame_shock,previous_action=lava_burst,if=buff.unleash_flame.up
	#/searing_totem
	if TotemExpires(fire) Spell(SEARINGTOTEM)
	#/stormstrike
	Spell(STORMSTRIKE)
	#/lava_lash
	Spell(LAVALASH)
	#/lightning_bolt,if=buff.maelstrom_weapon.react=5
	if BuffPresent(MAELSTROMWEAPON stacks=5) Spell(LIGHTNINGBOLT)
	#/unleash_elements
	Spell(UNLEASHELEMENTS)
	#/lava_burst,if=cooldown.shock.remains<cast_time&dot.flame_shock.remains>cast_time+travel_time&buff.unleash_flame.remains>cast_time
	if {spell(FLAMESHOCK) < castTime(LAVABURST)}
		and {target.debuffExpires(FLAMESHOCK mine=1) > castTime(LAVABURST) + 1}
		and {player.buffExpires(UNLEASHFLAME) > castTime(LAVABURST)}
		Spell(LAVABURST)
	#/flame_shock,if=!ticking|buff.unleash_flame.up
	if TargetDebuffExpires(FLAMESHOCK 0 mine=1) or BuffPresent(UNLEASHFLAME) Spell(FLAMESHOCK)
	#/earth_shock
	Spell(EARTHSHOCK)
	#/fire_nova,if=target.adds>1
	#/lightning_bolt,if=buff.maelstrom_weapon.react>1
	if CheckBoxOn(aoe) and BuffPresent(MAELSTROMWEAPON) Spell(CHAINLIGHTNING)
	if BuffPresent(MAELSTROMWEAPON) Spell(LIGHTNINGBOLT)
}

AddIcon help=cd mastery=2
{
	#/tolvir_potion,if=!in_combat
	if CheckBoxOn(potions) unless InCombat() Item(POTIONOFTHETOLVIR)
	#/wind_shear
	if TargetIsInterruptible(yes) Spell(WINDSHEAR)
	#/tolvir_potion,if=buff.bloodlust.react|target.time_to_die<=40
	if CheckBoxOn(potions) and {BuffPresent(heroism) or TargetDeadIn(less 41)} Item(POTIONOFTHETOLVIR)
	Item(Trinket0Slot usable=1)
	Item(Trinket1Slot usable=1)
	#/spirit_wolf
	Spell(FERALSPIRIT)
	Spell(FIREELEMENTALTOTEM)
	#/earth_elemental_totem
	Spell(EARTHELEMENTALTOTEM)
	#/spiritwalkers_grace,moving=1
	if Speed(more 0) Spell(SPIRITWALKERSGRACE)
}

AddIcon help=mana size=small
{
	Spell(SHAMANISTICRAGE)
	Spell(THUNDERSTORM)
}

AddIcon help=cd size=small
{
	# survival CD
	if Glyph(GLYPHOFSTONECLAWTOTEM yes) Spell(STONECLAWTOTEM)

	unless BuffPresent(heroism)
	{
		#/bloodlust,health_percentage<=25
		#/bloodlust,if=target.time_to_die<=60
		if TargetLifePercent(less 25) or TargetDeadIn(less 60)
		{
			Spell(BLOODLUST)
			Spell(HEROISM)
		}
	}
}